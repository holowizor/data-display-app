plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'application'
}

group 'com.devbuild'
version '0.0.1'
mainClassName = "com.devbuild.ddapp.MainKt"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://maven.pkg.github.com/holowizor/repository'
        credentials {
            username = project.findProperty("gpr.user") as String ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") as String ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.3"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.3"
    api "com.devbuild:waveshare213v2:0.0.1"

    api "org.slf4j:slf4j-api:1.7.30"
    api "ch.qos.logback:logback-classic:1.2.3"
    api "ch.qos.logback:logback-core:1.2.3"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task uberJar(type: Jar) {
    archiveClassifier = 'uber'

    from sourceSets.main.output

    manifest {
        attributes "Implementation-Title": "Devbuild app",
                "Main-Class": "${mainClassName}"
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}